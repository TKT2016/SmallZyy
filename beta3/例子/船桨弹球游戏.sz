导入类:常用颜色
属性:SCORE = 0,JX=矩形(120,12),TY=椭圆(8,8)
启动:	
	图形管理器添加JX
	图形管理器添加TY

	图形窗口的字体大小 = 14
	图形窗口的鼠标移动操作 = 响应鼠标移动
	
	显示得分
	播放器同步播放音频"BellRing.wav"

	x = 0
	y = 0
	deltaX = 1
	deltaY = 2
	gw = 图形窗口的长度
	gh = 图形窗口的高度

	当(1==1)重复
	  x = x + deltaX
	  y = y + deltaY
	  如果 (x >= gw - 16 或者 x <= 0)
		deltaX = -deltaX
	  如果 (y <= 0)
		deltaY = -deltaY
	  
	  图形管理器根据JX的序号获取左边距=> padX
	  如果 (y == gh - 28 并且 x >= padX 并且 x <= padX + 120) 
		播放器异步播放音频"Click.wav"
		SCORE = SCORE + 10
		显示得分
		deltaY = -deltaY
	  图形管理器根据TY的序号移动图形到点(x,y)
	  程序暂停5毫秒

	//图形窗口弹出消息("你的得分是: " + SCORE, "Paddle")

响应鼠标移动:
  图形管理器根据JX的序号移动图形到点(图形窗口的鼠标X坐标 - 60,图形窗口的高度 - 12)

显示得分: 
  图形窗口的笔刷颜色=白色
  J1=矩形（200,20）
  J1的左上角位置=点(10,10)
  图形窗口填充J1
  图形窗口的笔刷颜色=黑色
  WZK = 文字块("得分:"+SCORE,点(10,10))
  //WZK的位置=点(10,10)
  图形窗口绘制WZK
